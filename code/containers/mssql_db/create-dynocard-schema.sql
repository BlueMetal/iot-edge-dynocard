/******************************************************
 *
 * Name:         create-dynocard-schema.sql
 *     
 * Design Phase:
 *     Author:   John Miner
 *     Date:     04-01-2018
 *     Purpose:  Create the schema for the [db4cards] database.
 * 
 ******************************************************/

-- Assumes that the [db4cards] database has already been created
-- Ensure that [db4cards] database is being used

-- Service Accounts
CREATE USER [dyno_user] WITH PASSWORD = 'GMZNAQ]Q6R6Ljz9>';
--CREATE USER [dyno_user] WITH PASSWORD = 'GMZNAQdQ6R6Ljz97';

-- Give rights
EXEC sp_addrolemember 'db_owner', 'dyno_user'  
EXEC sp_addrolemember 'db_datareader', 'dyno_pbi_user'
--
-- Create ACTIVE schema
--

-- Delete existing schema.
DROP SCHEMA IF EXISTS [ACTIVE]
GO
 
-- Add new schema.
CREATE SCHEMA [ACTIVE] AUTHORIZATION [dbo]
GO

--
-- Create STAGE schema
--

-- Delete existing schema.
DROP SCHEMA IF EXISTS [STAGE]
GO
 
-- Add new schema.
CREATE SCHEMA [STAGE] AUTHORIZATION [dbo]
GO

--
-- Create PUMP table
--
 
-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[PUMP]
GO
 
-- Create new table
CREATE TABLE [ACTIVE].[PUMP]
(
  PU_ID INT IDENTITY(1, 1) NOT NULL,
  PU_TAG VARCHAR(32) NOT NULL,
  PU_INSTALLED DATE NOT NULL,
  PU_LONGITUDE REAL NULL,
  PU_LATITUDE REAL NULL,
  PU_DESCRIPTION VARCHAR(128) NULL,
  PU_UPDATE_DATE DATETIME CONSTRAINT [DF_PUMP_UPD_DATE] DEFAULT (getdate()) ,
  PU_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_PUMP_UPD_BY] DEFAULT (coalesce(suser_sname(),'?')),
  CONSTRAINT [PK_PUMP_ID] PRIMARY KEY CLUSTERED (PU_ID ASC)
)
GO

INSERT INTO [ACTIVE].[PUMP]
           ([PU_TAG]
           ,[PU_INSTALLED])
VALUES ('simPump', GetDate())
GO

--
-- Create DYNO CARD table
--

-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[DYNO_CARD]
GO
 
-- Create new table
CREATE TABLE [ACTIVE].[DYNO_CARD]
(
  DC_ID INT IDENTITY(1, 1) NOT NULL,
  PU_ID INT NOT NULL,
  DC_UPDATE_DATE DATETIME CONSTRAINT [DF_DYNO_CARD_DATE] DEFAULT (getdate()) ,
  DC_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_DYNO_CARD_BY] DEFAULT (coalesce(suser_sname(),'?')),
  CONSTRAINT [PK_DYNO_CARD_ID] PRIMARY KEY CLUSTERED (DC_ID ASC)
)
GO

-- Add foreign key 
ALTER TABLE [ACTIVE].[DYNO_CARD] WITH CHECK 
  ADD CONSTRAINT [FK_PUMP_ID1] FOREIGN KEY([PU_ID])
  REFERENCES [ACTIVE].[PUMP] ([PU_ID])
GO

--
-- Create EVENT table
--

-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[EVENT]
GO
 
-- Create new table
CREATE TABLE [ACTIVE].[EVENT]
(
  EV_ID INT IDENTITY(1, 1) NOT NULL,
  PU_ID INT NOT NULL,
  EV_EPOC_DATE INT NULL,
  EV_ANOMALY_ID UNIQUEIDENTIFIER NULL,
  EV_UPDATE_DATE DATETIME CONSTRAINT [DF_EVENT_UPD_DATE] DEFAULT (getdate()) ,
  EV_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_EVENT_UPD_BY] DEFAULT (coalesce(suser_sname(),'?')),
  CONSTRAINT [PK_EVENT_ID] PRIMARY KEY CLUSTERED (EV_ID ASC)
)
GO

-- Add foreign key 
ALTER TABLE [ACTIVE].[EVENT] WITH CHECK 
  ADD CONSTRAINT [FK_PUMP_ID2] FOREIGN KEY([PU_ID])
  REFERENCES [ACTIVE].[PUMP] ([PU_ID])
GO

--
-- Create EVENT DETAIL table
--

-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[EVENT_DETAIL]
GO
 

CREATE TABLE [ACTIVE].[EVENT_DETAIL]
(
    ED_ID [int] IDENTITY(1, 1) NOT NULL,
    EV_ID [int] NOT NULL,
    DC_ID [int] NOT NULL, 
    ED_TRIGGERED_EVENTS BIT CONSTRAINT [DF_EVENT_DETAIL_TRIGGERED_EVENTS] DEFAULT (0),
    ED_UPDATE_DATE DATETIME CONSTRAINT [DF_EVENT_DETAIL_UPD_DATE] DEFAULT (getdate()) ,
    ED_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_EVENT_DETAIL_UPD_BY] DEFAULT (coalesce(suser_sname(),'?')),
    CONSTRAINT [PK_EVENT_DETAIL_ID] PRIMARY KEY CLUSTERED (ED_ID ASC)
)
GO

-- Add foreign key 
ALTER TABLE [ACTIVE].[EVENT_DETAIL] WITH CHECK 
  ADD CONSTRAINT [FK_EVENT_ID] FOREIGN KEY([EV_ID])
  REFERENCES [ACTIVE].[EVENT] ([EV_ID])
GO

-- Add foreign key 
ALTER TABLE [ACTIVE].[EVENT_DETAIL] WITH CHECK 
  ADD CONSTRAINT [FK_DYNO_CARD_ID2] FOREIGN KEY([DC_ID])
  REFERENCES [ACTIVE].[DYNO_CARD] ([DC_ID])
GO

--
-- Create CARD HEADER table
--

-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[CARD_HEADER]
GO
 
-- Create new table
CREATE TABLE [ACTIVE].[CARD_HEADER]
(
  CH_ID INT IDENTITY(1, 1) NOT NULL,
  DC_ID INT NOT NULL,
  CH_EPOC_DATE INT NOT NULL,
  CH_SCALED_MAX_LOAD REAL NULL,
  CH_SHUTDOWN_EVENT_ID INT NULL,
  CH_NUMBER_OF_POINTS INT NULL,
  CH_GROSS_STROKE REAL NULL,
  CH_NET_STROKE REAL NULL,
  CH_PUMP_FILLAGE REAL NULL,
  CH_FLUID_LOAD REAL NULL,
  CH_SCALED_MIN_LOAD REAL NULL,
  CH_STROKE_LENGTH REAL NULL,
  CH_STROKE_PERIOD REAL NULL,
  CH_CARD_TYPE CHAR(1) NULL,
  CH_UPDATE_DATE DATETIME CONSTRAINT [DF_CARD_HDR_UPD_DATE] DEFAULT (getdate()) ,
  CH_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_CARD_HDR_UPD_BY] DEFAULT (coalesce(suser_sname(),'?')),
  CONSTRAINT [PK_CARD_HEADER_ID] PRIMARY KEY CLUSTERED (CH_ID ASC)
)
GO

-- Add foreign key 
ALTER TABLE [ACTIVE].[CARD_HEADER] WITH CHECK 
  ADD CONSTRAINT [FK_DYNO_CARD_ID1] FOREIGN KEY([DC_ID])
  REFERENCES [ACTIVE].[DYNO_CARD] ([DC_ID])
GO

--
-- Create CARD DETAIL table
--

-- Delete existing table
DROP TABLE IF EXISTS [ACTIVE].[CARD_DETAIL]
GO
 
-- Create new table
CREATE TABLE [ACTIVE].[CARD_DETAIL]
(
  CD_ID INT IDENTITY(1, 1) NOT NULL,
  CH_ID INT NOT NULL,
  CD_EDGE_ID INT NULL,
  CD_POSITION REAL NOT NULL,
  CD_LOAD REAL NOT NULL,
  CD_UPDATE_DATE DATETIME CONSTRAINT [DF_CARD_DTL_UPD_DATE] DEFAULT (getdate()) ,
  CD_UPDATE_BY VARCHAR(128) CONSTRAINT [DF_CARD_DTL_UPD_BY] DEFAULT (coalesce(suser_sname(),'?')),
  CONSTRAINT [PK_CARD_DETAIL_ID] PRIMARY KEY CLUSTERED (CD_ID ASC)
);


-- Add foreign key 
ALTER TABLE [ACTIVE].[CARD_DETAIL] WITH CHECK 
  ADD CONSTRAINT [FK_HEADER_ID] FOREIGN KEY([CH_ID])
  REFERENCES [ACTIVE].[CARD_HEADER] ([CH_ID])
GO


--
-- Create SURFACE DATA table
--

-- Delete existing table
DROP TABLE IF EXISTS [STAGE].[SURFACE_DATA]
GO
 
-- Create new table
CREATE TABLE [STAGE].[SURFACE_DATA]
(
  SD_ID INT IDENTITY(1, 1) NOT NULL,
  SD_TAG VARCHAR(32) NOT NULL,
  SD_POSITION REAL NOT NULL,
  SD_LOAD REAL NOT NULL,
  CONSTRAINT [PK_SURFACE_DATA_ID] PRIMARY KEY CLUSTERED (SD_ID ASC)
);

--
-- Create PUMP DATA table
--

-- Delete existing table
DROP TABLE IF EXISTS [STAGE].[PUMP_DATA]
GO
 
-- Create new table
CREATE TABLE [STAGE].[PUMP_DATA]
(
  PD_ID INT IDENTITY(1, 1) NOT NULL,
  PD_TAG VARCHAR(32) NOT NULL,
  PD_SECS REAL NOT NULL,
  PD_POSITION REAL NOT NULL,
  PD_LOAD REAL NOT NULL,
  CONSTRAINT [PK_PUMP_DATA_ID] PRIMARY KEY CLUSTERED (PD_ID ASC)
);

DROP VIEW IF EXISTS [ACTIVE].[DYNO_CARD_ANOMALY_VIEW]
GO

CREATE VIEW [ACTIVE].[DYNO_CARD_ANOMALY_VIEW] AS

	SELECT
		   DC.PU_ID as Pump_ID, 
		   ED.ED_ID as Event_ID,
		   CD.CH_ID as CardHeader_ID,
		   CH.CH_CARD_TYPE as Card_Type,
		   CH.CH_EPOC_DATE as EPOC_DATE,
		   CD.CD_EDGE_ID as Card_ID,
		   CD.CD_POSITION as Postion,
		   CD.CD_LOAD as Load
	FROM
		  ACTIVE.CARD_DETAIL CD
		  INNER JOIN  ACTIVE.CARD_HEADER CH
			  ON CH.CH_ID = CD.CH_ID
		  INNER JOIN  ACTIVE.DYNO_CARD DC
			 ON DC.DC_ID=CH.DC_ID
		  INNER JOIN ACTIVE.EVENT_DETAIL ED
			 ON DC.DC_ID = ED.DC_ID

GO